plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://maven.fabricmc.net/' }
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.neoforged.net/releases/' }
        maven { url = 'https://maven.architectury.dev/' }
        maven {
            url = 'https://api.modrinth.com/maven'
            content { includeGroup 'maven.modrinth' }
        }
        maven {
            url = 'https://cursemaven.com'
            content { includeGroup 'curse.maven' }
        }
        maven {
            url = 'https://maven2.bai.lol'
            content {
                includeGroup 'lol.bai'
                includeGroup 'mcp.mobius.waila'
            }
        }
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment}@zip")
        }

        compileOnly 'org.jetbrains:annotations:26.0.1'

        // WTHIT/Jade API support (compileOnly for optional compatibility)
        modCompileOnly "mcp.mobius.waila:wthit-api:fabric-12.8.2"
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
        it.options.encoding = 'UTF-8'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        repositories {
            mavenLocal()
            maven {
                url = uri(rootProject.file('repo'))
            }
        }
    }
}
