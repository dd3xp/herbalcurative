plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

loom {
    runs {
        datagen {
            data()
            programArg '--all'
            programArg '--mod'
            programArg 'herbalcurative'
            programArg '--output'
            programArg file("${project(':common').projectDir}/src/main/generated/resources").absolutePath
            programArg '--existing'
            programArg file("${project(':common').projectDir}/src/main/resources").absolutePath
        }
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    // Optional WTHIT/Jade support
    modCompileOnly "mcp.mobius.waila:wthit-api:neo-12.8.2"
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'minecraft_version', rootProject.minecraft_version
    inputs.property 'minecraft_version_range', rootProject.minecraft_version_range
    inputs.property 'neoforge_version', rootProject.neoforge_version
    inputs.property 'loader_version_range', rootProject.loader_version_range
    inputs.property 'mod_id', rootProject.mod_id
    inputs.property 'mod_name', rootProject.mod_name
    inputs.property 'mod_license', rootProject.mod_license
    inputs.property 'mod_version', project.version
    inputs.property 'mod_authors', rootProject.mod_authors
    inputs.property 'mod_description', rootProject.mod_description

    filesMatching('META-INF/neoforge.mods.toml') {
        expand(
            version: project.version,
            minecraft_version: rootProject.minecraft_version,
            minecraft_version_range: rootProject.minecraft_version_range,
            neo_version: rootProject.neoforge_version,
            loader_version_range: rootProject.loader_version_range,
            mod_id: rootProject.mod_id,
            mod_name: rootProject.mod_name,
            mod_license: rootProject.mod_license,
            mod_version: project.version,
            mod_authors: rootProject.mod_authors,
            mod_description: rootProject.mod_description
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
    exclude 'architectury.common.json', '.cache/**'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
